buildscript {
	repositories {
		mavenCentral()
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:7.0.3'
	}
}

repositories {
	mavenCentral()
	google()
}

buildDir = "../build/android/${project.name}"

apply plugin: 'com.android.application'

dependencies {
	implementation project(':android-project:SDL2')
	implementation 'androidx.appcompat:appcompat:1.3.0'
	implementation 'com.google.android.material:material:1.4.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
	implementation 'com.google.android.gms:play-services-basement:18.0.1'
}

android {
	compileSdkVersion 31

	defaultConfig {
		minSdk 16
		targetSdkVersion 31
		applicationId 'com.platymuus.hamsandwich'
		externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
		ndk.abiFilters 'armeabi-v7a' //, 'arm64-v8a', 'x86', 'x86_64'
	}

	externalNativeBuild.cmake {
		version '3.21.2'
		path '../CMakeLists.txt'
		buildStagingDirectory "${buildDir}.staging"
	}

	sourceSets.main {
		assets.srcDir "../assets/android"
		// Our CMake build puts assets to embed in the .apk here at build time.
		assets.srcDir "${buildDir}/intermediates/cxx/hamsandwich-assets"
	}
}

// Building this project depends on building SDL2 first.
tasks.configureEach { task ->
	if (task.name == 'buildCMakeDebug') {
		task.dependsOn ':android-project:SDL2:externalNativeBuildDebug'
	} else if (task.name == 'buildCMakeRelWithDebInfo') {
		task.dependsOn ':android-project:SDL2:externalNativeBuildRelease'
	}
}
